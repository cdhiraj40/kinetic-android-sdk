plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id "org.openapi.generator" version "6.2.0"
    id 'maven-publish'
    id 'de.undercouch.download'
}

android {
    namespace = "org.kin.kinetic"

    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9"
    implementation files('libs/borshj-0.0.0.jar')
    implementation("com.squareup.moshi:moshi-kotlin:1.13.0")
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "com.squareup.okhttp3:okhttp:4.9.3"

    api 'com.github.metaplex-foundation.SolanaKT:solana:1.2'

    implementation 'com.google.code.gson:gson:2.8.6'
}


task downloadApiSwagger(type: Download) {
    src "https://raw.githubusercontent.com/kin-labs/kinetic/main/api-swagger.json"
    dest "$rootDir/api-swagger.json"
}

task generate(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    def outputDir = "$rootDir/kinetic/src/main/java/openapi"
    def targetDir = "$rootDir/kinetic/src/main/java/org/kin/kinetic/generated"
    def templateDir = "$rootDir/.openapi-generator/templates/kotlin-client"

    delete "$buildDir"

    dependsOn(downloadApiSwagger)
    apiPackage = "generated"
    generatorName = "kotlin"
    inputSpec = "$rootDir/api-swagger.json"
    outputDir = "$outputDir"
    templateDir = "$templateDir"
    configOptions = [
            dateLibrary: "java8",
    ]

    doLast {
        ant.move file: "${outputDir}", todir: "$targetDir"
    }
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/kotlin"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "org.kin"
            artifactId = "kinetic"
            version = "1.0.0-rc.3"

            afterEvaluate {
                from components.release
            }
        }
    }
}